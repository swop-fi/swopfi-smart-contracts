{-# STDLIB_VERSION 5 #-}
{-# CONTENT_TYPE DAPP #-}
{-# SCRIPT_TYPE ACCOUNT #-}

let version = "2.0.0"

let kVersion = "version"
let kActive = "active"
let kAssetIdA = "A_asset_id"
let kAssetIdB = "B_asset_id"
let kBalanceA = "A_asset_balance"
let kBalanceB = "B_asset_balance"
let kShareAssetId = "share_asset_id"
let kShareAssetSupply = "share_asset_supply"
let kFee = "commission"
let kFeeScaleDelimiter = "commission_scale_delimiter"
let kInvariant = "invariant"
let kCause = "shutdown_cause"
let keyUSDNNSBTAddress = "staking_usdnnsbt_address"
let keyEURNAddress = "staking_eurn_address"

let keyAdminPubKey1 = "admin_pub_1"
let keyAdminPubKey2 = "admin_pub_2"
let keyAdminPubKey3 = "admin_pub_3"

let USDNToWavesExchanger = Address(base58'3PHaNgomBkrvEL2QnuJarQVJa71wjw9qiqG')

#TODO change address
let oracle = Address(base58'3PEbqViERCoKnmcSULh6n2aiMvUdSQdCsom')

func getAdminPub(keyAdminPub: String) = match oracle.getString(keyAdminPub) {
    case string: String => string.fromBase58String()
    case nothing => throw("Admin public key is empty")
}

let adminPubKey1 = getAdminPub(keyAdminPubKey1)
let adminPubKey2 = getAdminPub(keyAdminPubKey2)
let adminPubKey3 = getAdminPub(keyAdminPubKey3)
let admStartStop = base58'EtVkT6ed8GtbUiVVEqdmEqsp2J4qbb3rre2HFgxeVYdg'
let admStaking = base58'Czn4yoAuUZCVCLJDRfskn8URfkwpknwBTZDbs1wFrY7h'

let walletAddress = Address(base58'3P6J84oH51DzY6xk2mT5TheXRbrCwBMxonp')

let USDN = base58'DG2xFkPdDwKUoBkzGAhQtLpSGzfXLiCYPEzeKH2Ad24p'
let EURN = base58'DUk2YTxhRoAqMJLus4G2b3fR8hMHVh6eiyFx5r29VR6t'
let stakingAssets = [USDN.toBase58String(), EURN.toBase58String()]
let stakingUSDNNSBTAddress = Address(oracle.getString(keyUSDNNSBTAddress).valueOrErrorMessage("no usdn staking address").fromBase58String())
let stakingEURNAddress = Address(oracle.getString(keyEURNAddress).valueOrErrorMessage("no usdn staking address").fromBase58String())

let active = this.getBooleanValue(kActive)

let strAssetIdA = this.getStringValue(kAssetIdA)
let strAssetIdB = this.getStringValue(kAssetIdB)
let assetIdA = if strAssetIdA == "WAVES" then unit else strAssetIdA.fromBase58String()
let assetIdB = if strAssetIdB == "WAVES" then unit else strAssetIdB.fromBase58String()
let assetNameA = match assetIdA {
    case id: ByteVector => assetInfo(id).value().name
    case waves: Unit => "WAVES"
}
let assetNameB = match assetIdB {
    case id: ByteVector => assetInfo(id).value().name
    case waves: Unit => "WAVES"
}
let balanceA = this.getIntegerValue(kBalanceA)
let balanceB = this.getIntegerValue(kBalanceB)
let shareAssetId = this.getStringValue(kShareAssetId).fromBase58String()
let shareAssetSupply = this.getIntegerValue(kShareAssetSupply)
let invariant = this.getIntegerValue(kInvariant)

let fee = 500 # fee/feeScale6 = 0.0005
let feeGovernance = 200 # feeGovernance/feeScale6 = 0.0002. 3/5 fee back to the dApp, 2/5 sends to the governance address
let feeScale6 = 1000000

let scale3 = 1000
let scale8 = 100000000
let scale16 = 10000000000000000
let slippageScale3 = 1000
let digits8 = 8

let dAppThreshold = 50 # dAppThresholdAmount/dAppThresholdAmountDelimiter = 0.5
let dAppThresholdScale2 = 100

let exchangeRatioLimitMin = 90000000 # 0.9*scale8. This parameter helps to avoid losses when an incorrect argument is passed
let exchangeRatioLimitMax = 110000000 #1.1*scale8. This parameter helps to avoid losses when an incorrect argument is passed

let alpha = 50 # model coefficient alpha = 0.15 with 2 digits
let alphaDigits = 2
let beta = 4600000000000000 # model coefficient beta = 0.46 with 16 digits

func accountBalance(assetId: ByteVector|Unit) = match assetId {
    case id: ByteVector => this.assetBalance(id)
    case waves: Unit => this.wavesBalance().available
}

func stakedAmount(assetId: ByteVector|Unit) = {
    let stakedAmountCalculated = match assetId {
        case aId: ByteVector =>
          if aId == USDN  then {
              stakingUSDNNSBTAddress.getInteger("rpd_balance_" + aId.toBase58String() + "_" + this.toString())
          } else if  aId == EURN then {
              stakingEURNAddress.getInteger("%s%s%s__stakingBalance__" + aId.toBase58String() + "__" + this.toString())
          } else 0
        case _:Unit => 0
      }
    match (stakedAmountCalculated) {
        case i:Int => i
        case _ => 0
    }
}

let stakedAmountA = stakedAmount(assetIdA)
let stakedAmountB = stakedAmount(assetIdB)
let availableBalanceA = balanceA - stakedAmountA
let availableBalanceB = balanceB - stakedAmountB
let accountBalanceWithStakedA = accountBalance(assetIdA) + stakedAmountA
let accountBalanceWithStakedB = accountBalance(assetIdB) + stakedAmountB

let hasEnoughBalance = accountBalanceWithStakedA >= balanceA && accountBalanceWithStakedB >= balanceB

# skewness = 0.5*(x/y+y/x)
# Each fraction multiple by scale in order to avoid zeroing
func skewness(x: Int, y: Int) = (scale16.fraction(x, y) + scale16.fraction(y, x)) / 2

# Calculate (x+y)*skewness^(-alpha) + 2*(x*y)^0.5*(skewness-beta)^alpha
func invariantCalc(x: Int, y: Int) = {
    let sk = skewness(x, y)
    fraction(
        x + y,
        scale16,
        pow(sk, digits8, alpha, alphaDigits, digits8, CEILING)
    ) + 2 * fraction(
        pow(fraction(x.toBigInt(), y.toBigInt(), scale8.toBigInt()), 0, 5.toBigInt(), 1, digits8 / 2, DOWN).toInt(),
        pow(sk - beta, digits8, alpha, alphaDigits, digits8, DOWN),
        scale8
    )
}

func calculateSendAmount(amountToSendEstimated: Int, minTokenReceiveAmount: Int, tokenReceiveAmount: Int, tokenId: ByteVector|Unit) = {
    let worstAllowedNewInvariantRatio = scale16 - scale16 * 1 / 10000000 # 0.000001% of slippage
    let deltaBetweenMaxAndMinSendValue = amountToSendEstimated - minTokenReceiveAmount

    # only one of the variables will be used depending on the token
    let x = balanceA + tokenReceiveAmount
    let y = balanceB + tokenReceiveAmount

    func getStepAmount(acc: (Int, Int), step: Int) = {
      if acc._1 == -1 then
        let amountToSend = amountToSendEstimated - step * deltaBetweenMaxAndMinSendValue / 3 / scale3 # ATTENTION - Magic number in formula
        let stepInvariant =
            if tokenId == assetIdA then
                invariantCalc(x, balanceB - amountToSend)
            else invariantCalc(balanceA - amountToSend, y)

        if stepInvariant > invariant then
            (amountToSend, stepInvariant)
        else
          (-1, 0)
      else
        acc
    }

    let amountToSendMin = getStepAmount((-1, 0), 3000)
    if amountToSendMin._1 < 0 then
        throw("Price is worse than minReceived")
    else
        let invEstimated = if tokenId == assetIdA then
                invariantCalc(x, balanceB - amountToSendEstimated)
            else if tokenId == assetIdB then
                invariantCalc(balanceA - amountToSendEstimated, y)
            else throw("Wrong asset in payment")

        if invEstimated < invariant then
            # actual price is worse than estimated. Try to find matching price
            if (scale16.fraction(invariant, invEstimated) <= worstAllowedNewInvariantRatio) then
                throw("The requested price is too not profitable for user")
                else #
                    let a = FOLD<3>([25, 200, 500], (-1, 0), getStepAmount)
                    if (a._1 < 0) then
                        amountToSendMin._1
                    else
                        a._1
        else
            # actual price is better than estimated. Try to find even better price
            let a = FOLD<3>([-500, -200, -25], (-1, 0), getStepAmount)
            let tupleSendBetter = if (a._1 < 0) then
                    (amountToSendEstimated, invEstimated)
                else
                    a
            if (scale16.fraction(invariant, tupleSendBetter._2) <= worstAllowedNewInvariantRatio) then
                    throw("The requested price is too not profitable for user")
                else
                    tupleSendBetter._1
}

func getAssetInfo(assetId: ByteVector|Unit) = match assetId {
    case id: ByteVector =>
        let stringId = id.toBase58String()
        let info = assetInfo(id).valueOrErrorMessage("Asset " + stringId + " doesn't exist")
        (stringId, info.name, info.decimals)
    case waves: Unit => ("WAVES", "WAVES", 8)
}

func suspend(cause: String) = [
    BooleanEntry(kActive, false),
    StringEntry(kCause, cause)
]

func throwIsActive() = throw("DApp is already active")
func isActive() = if active then unit else throw("DApp is inactive at this moment")
func isAdminCall(i:Invocation) = if [adminPubKey1, adminPubKey2, adminPubKey3].containsElement(i.callerPublicKey) then unit
        else throw("Only admin can call this function")
func throwAssets() = throw("Incorrect assets attached. Expected: " + strAssetIdA + " and " + strAssetIdB)
func throwOnePayment() = throw("One attached payment expected")
func throwThreshold(threshold: Int, amountA: Int, amountB: Int) = throw("New balance in assets of the DApp is less than threshold " + threshold.toString()
    + ": " + amountA.toString() + " " + assetNameA + ", " + amountB.toString() + " " + assetNameB)
func suspendSuspicious() = suspend("Suspicious state. Actual balances: " + balanceA.toString() + " " + assetNameA + ", " + balanceB.toString() + " " + assetNameB
        + ". State: " + accountBalance(assetIdA).toString() + " " + assetNameA + ", " + accountBalance(assetIdB).toString() + " " + assetNameB)

func calcStakingFuncAndAddres(stake: Boolean, assetId: ByteVector) = {
    if stake then
        if assetId == USDN then ("lockNeutrino", stakingUSDNNSBTAddress)
        else ("startStaking", stakingEURNAddress)
    else
        if assetId == USDN then ("unlockNeutrino", stakingUSDNNSBTAddress)
        else ("stopStaking", stakingEURNAddress)
}

func calcStakingParams(stake: Boolean, amount:Int, assetId: ByteVector) = {
    if stake then
        let (call, stakingAddr) = calcStakingFuncAndAddres(stake, assetId)
        (call, stakingAddr, [], [AttachedPayment(assetId, amount)])
    else
        let (call, stakingAddr) = calcStakingFuncAndAddres(stake, assetId)
        (call, stakingAddr, [amount, assetId.toBase58String()], [])
}

@Callable(i)
func init() = {
    let (pmtAmountA, pmtAssetIdA) = (i.payments[0].amount, i.payments[0].assetId)
    let (pmtAmountB, pmtAssetIdB) = (i.payments[1].amount, i.payments[1].assetId)
    let (pmtStrAssetIdA, pmtAssetNameA, pmtDecimalsA) = getAssetInfo(pmtAssetIdA)
    let (pmtStrAssetIdB, pmtAssetNameB, pmtDecimalsB) = getAssetInfo(pmtAssetIdB)

    if this.getBoolean(kActive).isDefined() then
        throwIsActive()
    else if pmtAssetIdA == pmtAssetIdB then
        throw("Assets must be different")
    else {
        let shareName = "s" + pmtAssetNameA.take(7) + "_" + pmtAssetNameB.take(7)
        let shareDescription = "ShareToken of SwopFi protocol for " + pmtAssetNameA + " and " + pmtAssetNameB + " at address " + this.toString()

        # we save 16 digit info with using digits8 in pow functions. We divide /scale8 to delete 8 digits and another 8 digits we define as digits in shareToken in Issue tx
        let shareDecimals = (pmtDecimalsA + pmtDecimalsB) / 2
        let shareInitialSupply = fraction(
            pow(pmtAmountA, pmtDecimalsA, 5, 1, pmtDecimalsA, DOWN),
            pow(pmtAmountB, pmtDecimalsB, 5, 1, pmtDecimalsB, DOWN),
            pow(10, 0, shareDecimals, 0, 0, DOWN)
        )
        let shareIssue = Issue(shareName, shareDescription, shareInitialSupply, shareDecimals, true)
        let shareIssueId = shareIssue.calculateAssetId()

        let invariantCalculated = invariantCalc(pmtAmountA, pmtAmountB)
        strict stake1 = if stakingAssets.containsElement(pmtStrAssetIdA) then
            invoke(this, "stakeUnstake", [true, pmtAmountA, pmtStrAssetIdA], []) else 0
        strict stake2 = if stakingAssets.containsElement(pmtStrAssetIdB) then
            invoke(this, "stakeUnstake", [true, pmtAmountB, pmtStrAssetIdB], []) else 0
        [
            StringEntry(kVersion, version),
            BooleanEntry(kActive, true),
            StringEntry(kAssetIdA, pmtStrAssetIdA),
            StringEntry(kAssetIdB, pmtStrAssetIdB),
            IntegerEntry(kBalanceA, pmtAmountA),
            IntegerEntry(kBalanceB, pmtAmountB),
            IntegerEntry(kInvariant, invariantCalculated),
            IntegerEntry(kFee, fee),
            IntegerEntry(kFeeScaleDelimiter, feeScale6),
            shareIssue,
            StringEntry(kShareAssetId, shareIssueId.toBase58String()),
            IntegerEntry(kShareAssetSupply, shareInitialSupply),
            ScriptTransfer(i.caller, shareInitialSupply, shareIssueId)
        ]
    }
}

# if someone ready to replenish in both tokens. It's necessary to bring tokens amount in proportion according to dApp state
@Callable(i)
func replenishWithTwoTokens() = isActive().valueOrElse({
    let pmtAssetIdA = i.payments[0].assetId
    let pmtAssetIdB = i.payments[1].assetId
    let ratioShareTokensInA = scale8.fraction(i.payments[0].amount, balanceA)
    let ratioShareTokensInB = scale8.fraction(i.payments[1].amount, balanceB)
    let (pmtStrAssetIdA, pmtAssetNameA, pmtDecimalsA) = getAssetInfo(pmtAssetIdA)
    let (pmtStrAssetIdB, pmtAssetNameB, pmtDecimalsB) = getAssetInfo(pmtAssetIdB)

    #calc change if ratio is not valid
    let (pmtAmountA, pmtAmountB, change, changeAssetId, shareTokenRatio) =
        if(ratioShareTokensInA < ratioShareTokensInB) then
            let pmt = fraction(balanceB, ratioShareTokensInA, scale8, CEILING)
            (i.payments[0].amount, pmt, i.payments[1].amount - pmt, pmtAssetIdB, ratioShareTokensInA)
        else
            let pmt = fraction(balanceA, ratioShareTokensInB, scale8, CEILING)
            (pmt, i.payments[1].amount, i.payments[0].amount - pmt, pmtAssetIdA, ratioShareTokensInB)

    let shareTokenToPayAmount = shareTokenRatio.fraction(shareAssetSupply, scale8)

    if i.payments.size() != 2 then
        throw("Two attached assets expected")
    else if pmtAssetIdA != assetIdA || pmtAssetIdB != assetIdB then
        throwAssets()
    else if shareTokenToPayAmount == 0 then
        throw("Too small amount to replenish")
    else if change < 0 then
        throw("Change < 0")
    else if !hasEnoughBalance then
        [
            ScriptTransfer(i.caller, pmtAmountA, pmtAssetIdA),
            ScriptTransfer(i.caller, pmtAmountB, pmtAssetIdB)
        ] ++ suspendSuspicious()
    else
        strict stake1 = if stakingAssets.containsElement(pmtStrAssetIdA) then
                invoke(this, "stakeUnstake", [true, pmtAmountA, pmtStrAssetIdA], []) else 0
        strict stake2 = if stakingAssets.containsElement(pmtStrAssetIdB) then
            invoke(this, "stakeUnstake", [true, pmtAmountB, pmtStrAssetIdB], []) else 0
        [
            IntegerEntry(kBalanceA, balanceA + pmtAmountA),
            IntegerEntry(kBalanceB, balanceB + pmtAmountB),
            IntegerEntry(kShareAssetSupply, shareAssetSupply + shareTokenToPayAmount),
            IntegerEntry(kInvariant, invariantCalc(balanceA + pmtAmountA, balanceB + pmtAmountB)),
            Reissue(shareAssetId, shareTokenToPayAmount, true),
            ScriptTransfer(i.caller, shareTokenToPayAmount, shareAssetId),
            ScriptTransfer(i.caller, change, changeAssetId)
        ]
})

# if someone ready to replenish in one token
@Callable(i)
func replenishWithOneToken(virtualSwapTokenPay: Int, virtualSwapTokenGet: Int) = isActive().valueOrElse({
    let (pmtAmount, pmtAssetId) = (i.payments[0].amount, i.payments[0].assetId)
    let (pmtStrAssetId, pmtAssetName, pmtDecimals) = getAssetInfo(pmtAssetId)

    let pmtMinThreshold = 5000000
    let thresholdValueForMinTolerance = 50000000
    let tolerance = if pmtAmount < thresholdValueForMinTolerance then 100000 else 1

    let slippageValueMinForReplenish = scale8 - scale8 * tolerance / 10000000 # if pmtAmount > 50.000000 then slippage = 0.000001%
    let slippageValueMaxForReplenish = scale8 + scale8 * tolerance / 10000000

    let slippageValueMinForSwap = scale8 - scale8 * 1 / 10000000

    if pmtAmount < pmtMinThreshold then
        throw("Payment amount " + pmtAmount.toString() + " does not exceed the minimum amount of " + pmtMinThreshold.toString() + " tokens")
    else if i.payments.size() != 1 then
        throwOnePayment()
    else if !hasEnoughBalance then
        [
            ScriptTransfer(i.caller, pmtAmount, pmtAssetId)
        ] ++ suspendSuspicious()
    else if pmtAssetId != assetIdA && pmtAssetId != assetIdB then
        throwAssets()
    else {
        let (virtualReplenishA, virtualReplenishB,
            balanceAfterSwapA, balanceAfterSwapB,
            newBalanceA, newBalanceB) =
            if pmtAssetId == assetIdA then (
                pmtAmount - virtualSwapTokenPay, virtualSwapTokenGet,
                balanceA + virtualSwapTokenPay, balanceB - virtualSwapTokenGet,
                balanceA + pmtAmount, balanceB
            ) else (
                virtualSwapTokenGet, pmtAmount - virtualSwapTokenPay,
                balanceA - virtualSwapTokenGet, balanceB + virtualSwapTokenPay,
                balanceA, balanceB + pmtAmount
            )

        let invariantNew = invariantCalc(balanceAfterSwapA, balanceAfterSwapB)
        let ratioVirtualBalanceToVirtualReplenish = (scale8 * scale8).fraction(balanceAfterSwapA, balanceAfterSwapB)
                                                    / scale8.fraction(virtualReplenishA, virtualReplenishB)
        let dAppThresholdAmount = fraction(newBalanceA + newBalanceB, dAppThreshold, 2 * dAppThresholdScale2)

        if scale8.fraction(invariant, invariantNew) <= slippageValueMinForSwap || invariantNew < invariant then
            throw("Incorrect virtualSwapTokenPay or virtualSwapTokenGet value")
        else if ratioVirtualBalanceToVirtualReplenish < slippageValueMinForReplenish || ratioVirtualBalanceToVirtualReplenish > slippageValueMaxForReplenish then
            throw("Swap with virtualSwapTokenPay and virtualSwapTokenGet is possible, but ratio after virtual swap is incorrect")
        else if newBalanceA < dAppThresholdAmount || newBalanceB < dAppThresholdAmount then
            throwThreshold(dAppThresholdAmount, newBalanceA, newBalanceB)
        else {
            # fee for staking operation
            let ratioShareTokensInA = virtualReplenishA.fraction(scale8, balanceAfterSwapA)
            let ratioShareTokensInB = virtualReplenishB.fraction(scale8, balanceAfterSwapB)

            let shareTokenToPayAmount = min([ratioShareTokensInA, ratioShareTokensInB]).fraction(shareAssetSupply, scale8)
             # fee for operation is devided by 2 to account internal swap commission. Only half of tokens took part in virtual exchange
            let shareTokenToPayAmountAfterFee = shareTokenToPayAmount.fraction(feeScale6 - fee/2, feeScale6)
            let shareTokenGovernanceReward = shareTokenToPayAmount.fraction(feeGovernance/2, feeScale6)
            let governanceRewardTokenA = shareTokenGovernanceReward.fraction(balanceA, shareAssetSupply)
            let governanceRewardTokenB = shareTokenGovernanceReward.fraction(balanceB, shareAssetSupply)

            let (stakeAmount, unstakeAmount, unstakeAsset) = if(pmtStrAssetId == strAssetIdA) then
                    (pmtAmount - governanceRewardTokenA, governanceRewardTokenB, strAssetIdB)
                else
                    (pmtAmount - governanceRewardTokenB, governanceRewardTokenA, strAssetIdA)
            strict stake1 = if stakingAssets.containsElement(pmtStrAssetId) then
                    invoke(this, "stakeUnstake", [true, stakeAmount, pmtStrAssetId], []) else 0

            strict stake2 = if stakingAssets.containsElement(unstakeAsset) then
                    invoke(this, "stakeUnstake", [false, unstakeAmount, unstakeAsset], []) else 0
            [
                Reissue(shareAssetId, shareTokenToPayAmountAfterFee, true),
                ScriptTransfer(i.caller, shareTokenToPayAmountAfterFee, shareAssetId),
                ScriptTransfer(walletAddress, governanceRewardTokenA, assetIdA),
                ScriptTransfer(walletAddress, governanceRewardTokenB, assetIdB),
                IntegerEntry(kBalanceA, newBalanceA - governanceRewardTokenA),
                IntegerEntry(kBalanceB, newBalanceB - governanceRewardTokenB),
                IntegerEntry(kShareAssetSupply, shareAssetSupply + shareTokenToPayAmountAfterFee),
                IntegerEntry(kInvariant, invariantCalc(newBalanceA - governanceRewardTokenA, newBalanceB - governanceRewardTokenB))
            ]
        }
    }
})

@Callable(i)
func withdraw() = isActive().valueOrElse({
    let (pmtAmount, pmtAssetId) = (i.payments[0].amount, i.payments[0].assetId)
    let amountToPayA = pmtAmount.fraction(balanceA, shareAssetSupply)
    let amountToPayB = pmtAmount.fraction(balanceB, shareAssetSupply)

    let invariantCalculated = invariantCalc(balanceA - amountToPayA, balanceB - amountToPayB)

    if i.payments.size() != 1 then
        throwOnePayment()
    else if pmtAssetId != shareAssetId then
        throw("Incorrect asset attached. Expected: " + shareAssetId.toBase58String())
    else if !hasEnoughBalance then
        [
            ScriptTransfer(i.caller, pmtAmount, pmtAssetId)
        ] ++ suspendSuspicious()
    else
        strict stake1 = if stakingAssets.containsElement(strAssetIdA) then
            invoke(this, "stakeUnstake", [false, amountToPayA, strAssetIdA], []) else 0
        strict stake2 = if stakingAssets.containsElement(strAssetIdB) then
            invoke(this, "stakeUnstake", [false, amountToPayB, strAssetIdB], []) else 0
        [
            IntegerEntry(kBalanceA, balanceA - amountToPayA),
            IntegerEntry(kBalanceB, balanceB - amountToPayB),
            IntegerEntry(kShareAssetSupply, shareAssetSupply - pmtAmount),
            IntegerEntry(kInvariant, invariantCalculated),
            Burn(shareAssetId, pmtAmount),
            ScriptTransfer(i.caller, amountToPayA, assetIdA),
            ScriptTransfer(i.caller, amountToPayB, assetIdB)
        ]
})

@Callable(i)
func exchange(estimatedAmountToReceive: Int, minAmountToReceive: Int) = isActive().valueOrElse({
    let (pmtAmount, pmtAssetId) = (i.payments[0].amount, i.payments[0].assetId)

    if estimatedAmountToReceive <= 0 then
        throw("Estimated amount must be positive. Actual: " + estimatedAmountToReceive.toString())
    else if minAmountToReceive > estimatedAmountToReceive then
        throw("Minimal amount can't be greater than estimated.")
    else if i.payments.size() != 1 then
        throwOnePayment()
    else if !hasEnoughBalance then
        [
            ScriptTransfer(i.caller, pmtAmount, pmtAssetId)
        ] ++ suspendSuspicious()
    else if pmtAssetId != assetIdA && pmtAssetId != assetIdB then
        throwAssets()
    else if pmtAmount < 10000000 then
        throw("Only swap of 10.000000 or more tokens is allowed")
    else if scale8.fraction(minAmountToReceive, pmtAmount) < exchangeRatioLimitMin ||
            scale8.fraction(estimatedAmountToReceive, pmtAmount) > exchangeRatioLimitMax then
        throw("Incorrect args and pmt ratio")
    else {
        let sendAssetId = if pmtAssetId == assetIdA then assetIdB else assetIdA

        let amount = calculateSendAmount(estimatedAmountToReceive, minAmountToReceive, pmtAmount, pmtAssetId)
        let governanceReward = amount.fraction(feeGovernance, feeScale6)
        let amountMinusFee = amount.fraction(feeScale6 - fee, feeScale6)

        let (newBalanceA, newBalanceB) =
            if pmtAssetId == assetIdA then
                (balanceA + pmtAmount, balanceB - amountMinusFee - governanceReward)
            else
                (balanceA - amountMinusFee - governanceReward, balanceB + pmtAmount)
        let dAppThresholdAmount = fraction(newBalanceA + newBalanceB, dAppThreshold, 2 * dAppThresholdScale2)

        if newBalanceA < dAppThresholdAmount || newBalanceB < dAppThresholdAmount then
            throwThreshold(dAppThresholdAmount, newBalanceA, newBalanceB)
        else
            let (pmtStrAssetId, pmtAssetName, pmtDecimals) = getAssetInfo(pmtAssetId)
            let (sendStrAssetId, sendAssetName, sendDecimals) = getAssetInfo(sendAssetId)
            strict stake1 = if stakingAssets.containsElement(pmtStrAssetId) then
                invoke(this, "stakeUnstake", [true, pmtAmount, pmtStrAssetId], []) else 0
            strict stake2 = if stakingAssets.containsElement(sendStrAssetId) then
                invoke(this, "stakeUnstake", [false, amountMinusFee + governanceReward, sendStrAssetId], []) else 0
            # successful execution result is updating information about actual balance and supply into the state and transfer tokens to the caller

            [
                IntegerEntry(kBalanceA, newBalanceA),
                IntegerEntry(kBalanceB, newBalanceB),
                IntegerEntry(kInvariant, invariantCalc(newBalanceA, newBalanceB)),
                ScriptTransfer(i.caller, amountMinusFee, sendAssetId),
                ScriptTransfer(walletAddress, governanceReward, sendAssetId)
            ]
    }
})

@Callable(i)
func shutdown() = isAdminCall(i).valueOrElse({
    if !active then
        throw("DApp is already suspended. Cause: " + this.getString(kCause).valueOrElse("the cause wasn't specified"))
    else
        suspend("Paused by admin")
})

@Callable(i)
func activate() = isAdminCall(i).valueOrElse({
    if active then
        throwIsActive()
    else [
        BooleanEntry(kActive, true),
        DeleteEntry(kCause)
    ]
})

@Callable(i)
func takeIntoAccountExtraFunds() = isActive().valueOrElse({
    let amountEnrollA = accountBalanceWithStakedA - balanceA
    let amountEnrollB = accountBalanceWithStakedB - balanceB
    let invariantNew = invariantCalc(balanceA + amountEnrollA, balanceB + amountEnrollB)

    if i.caller != walletAddress then
        throw("Only the wallet can call this function")
    else if amountEnrollA < 0 || amountEnrollB < 0 then
        suspend("Enroll amount negative")
    else if amountEnrollA == 0 && amountEnrollB == 0 then
        throw("No money to take")
    else
        strict stake1 = if stakingAssets.containsElement(strAssetIdA) && amountEnrollA > 0 then
            invoke(this, "stakeUnstake", [true, amountEnrollA, strAssetIdA], []) else 0
        strict stake2 = if stakingAssets.containsElement(strAssetIdB)  && amountEnrollB > 0 then
            invoke(this, "stakeUnstake", [true, amountEnrollB, strAssetIdB], []) else 0
        [
            IntegerEntry(kInvariant, invariantNew),
            IntegerEntry(kBalanceA, balanceA + amountEnrollA),
            IntegerEntry(kBalanceB, balanceB + amountEnrollB),
            IntegerEntry("last_income_"+strAssetIdA, amountEnrollA),
            IntegerEntry("last_income_"+strAssetIdB, amountEnrollB)
        ]
})


@Callable(i)
func stakeUnstake(stake:Boolean, amount: Int, assetIdString:String) = {
    if i.caller != this then
        throw("Only contract itself can invoke this function")
    else
        let (call, addr, params, payments) = calcStakingParams(stake, amount, assetIdString.fromBase58String())
        strict inv = invoke(addr, call, params, payments)
        []
}

@Verifier(tx)
func verify() = {
    let multiSignedByAdmins = {
        let adminPubKey1Signed = if sigVerify(tx.bodyBytes, tx.proofs[0], adminPubKey1) then 1 else 0
        let adminPubKey2Signed = if sigVerify(tx.bodyBytes, tx.proofs[1], adminPubKey2) then 1 else 0
        let adminPubKey3Signed = if sigVerify(tx.bodyBytes, tx.proofs[2], adminPubKey3) then 1 else 0
        adminPubKey1Signed + adminPubKey2Signed + adminPubKey3Signed >= 2
    }
    match tx {
        case inv: InvokeScriptTransaction =>
            let callTakeIntoAccount = inv.dApp == this && inv.function == "takeIntoAccountExtraFunds"
            let signedByAdmin =
                sigVerify(tx.bodyBytes, tx.proofs[0], adminPubKey1)
                || sigVerify(tx.bodyBytes, tx.proofs[0], adminPubKey2)
                || sigVerify(tx.bodyBytes, tx.proofs[0], adminPubKey3)
                || sigVerify(tx.bodyBytes, tx.proofs[0], admStaking)

            (callTakeIntoAccount && signedByAdmin)
            || multiSignedByAdmins
        case _ => multiSignedByAdmins
    }
}