{-# STDLIB_VERSION 4 #-}
{-# CONTENT_TYPE DAPP #-}
{-# SCRIPT_TYPE ACCOUNT #-}

let version = "2.0.0"

let kVersion = "version"
let kActive = "active"
let kAssetIdA = "A_asset_id"
let kAssetIdB = "B_asset_id"
let kBalanceA = "A_asset_balance"
let kBalanceB = "B_asset_balance"
let kShareAssetId = "share_asset_id"
let kShareAssetSupply = "share_asset_supply"
let kCommission = "commission"
let kFeeScaleDelimiter = "commission_scale_delimiter"
let kCommissionGovernance = "commission_governance"
let kInvariant = "invariant"
let kCause = "shutdown_cause"

let adm1 = base58'DXDY2itiEcYBtGkVLnkpHtDFyWQUkoLJz79uJ7ECbMrA'
let adm2 = base58'E6Wa1SGoktYcjHjsKrvjMiqJY3SWmGKcD8Q5L8kxSPS7'
let adm3 = base58'AZmWJtuy4GeVrMmJH4hfFBRApe1StvhJSk4jcbT6bArQ'
let admStartStop = base58'EtVkT6ed8GtbUiVVEqdmEqsp2J4qbb3rre2HFgxeVYdg'
let admStaking = base58'Czn4yoAuUZCVCLJDRfskn8URfkwpknwBTZDbs1wFrY7h'

let govAddr = Address(base58'3P6J84oH51DzY6xk2mT5TheXRbrCwBMxonp')
let stakingAddressUsdnAndNsbt = Address(base58'3PNikM6yp4NqcSU8guxQtmR5onr2D4e8yTJ')
let stakingAddressDeFo = Address(base58'3PFhcMmEZoQTQ6ohA844c7C9M8ZJ18P8dDj')

let USDN = base58'DG2xFkPdDwKUoBkzGAhQtLpSGzfXLiCYPEzeKH2Ad24p'
let NSBT = base58'6nSpVyNH7yM69eg446wrQR94ipbbcmZMU1ENPwanC97g'
let EURN = base58'DUk2YTxhRoAqMJLus4G2b3fR8hMHVh6eiyFx5r29VR6t'
let stakingAssets = [USDN, NSBT, EURN]

let isActive = this.getBooleanValue(kActive)

let strAssetIdA = this.getStringValue(kAssetIdA)
let strAssetIdB = this.getStringValue(kAssetIdB)
let assetIdA = if strAssetIdA == "WAVES" then unit else strAssetIdA.fromBase58String()
let assetIdB = if strAssetIdB == "WAVES" then unit else strAssetIdB.fromBase58String()
let assetNameA = match assetIdA {
    case id: ByteVector => assetInfo(id).value().name
    case waves: Unit => "WAVES"
}
let assetNameB = match assetIdB {
    case id: ByteVector => assetInfo(id).value().name
    case waves: Unit => "WAVES"
}
let balA = this.getIntegerValue(kBalanceA)
let balB = this.getIntegerValue(kBalanceB)
let shareAssetId = this.getStringValue(kShareAssetId).fromBase58String()
let shareAssetSupply = this.getIntegerValue(kShareAssetSupply)
let invt = this.getIntegerValue(kInvariant)

let fee = 500 # fee/feeScaleDelimiter = 0.0005
let feeGovernance = 200 # feeGovernance/feeScaleDelimiter = 0.0002. 3/5 fee back to the dApp, 2/5 sends to the governance address
let feeScaleDelimiter = 1000000

let scale3 = 1000
let scale8 = 100000000
let scale12 = 1000000000000
let slippageToleranceDelimiter = 1000
let scale8Digits = 8
let scale12Digits = 12

let dAppTHold = 50 # dAppTHoldAmount/dAppTHoldAmountDelimiter = 0.5
let dAppTHoldScale = 100

let ratioThresholdMax = 100000000
let ratioThresholdMin = 99999000

let alpha = 50 # model coefficient alpha = 0.15 with 2 digits
let alphaDigits = 2
let beta = 46000000 # model coefficient beta = 0.46 with 8 digits
let betaDigits = 8

func accountBalance(assetId: ByteVector|Unit) = match assetId {
    case id: ByteVector => this.assetBalance(id)
    case waves: Unit => this.wavesBalance().available
}

func stakedAmount(assetId: ByteVector|Unit) =
    match assetId {
        case id: ByteVector =>
            let stakingAmount =
                if [USDN, NSBT].containsElement(id) then
                    stakingAddressUsdnAndNsbt.getInteger("rpd_balance_" + id.toBase58String() + "_" + this.toString())
                else
                    stakingAddressDeFo.getInteger("%s%s%s__stakingBalance__" + id.toBase58String() + "__" + this.toString())
            match stakingAmount {
                case staked: Int => staked
                case nothing: Unit => 0
            }
        case waves => {
            let balances = this.wavesBalance()
            balances.regular - balances.available
        }
    }
let stakedAmountA = stakedAmount(assetIdA)
let stakedAmountB = stakedAmount(assetIdB)

let availBalA = balA - stakedAmountA
let availBalB = balB - stakedAmountB
let accBalWithStakedA = accountBalance(assetIdA) + stakedAmountA
let accBalWithStakedB = accountBalance(assetIdB) + stakedAmountB

let hasEnoughBalance = accBalWithStakedA >= balA && accBalWithStakedB >= balB

# skewness = 0.5*(x/y+y/x)
# Each fraction multiple by scale in order to avoid zeroing
# During working on this fraction scale8 was switched to scale12. To unscale back to 8 digits added /10000
func skewness(x: Int, y: Int) = (scale12.fraction(x, y) + scale12.fraction(y, x)) / 2 / 10000

# Calculate (x+y)*skewness^(-alpha) + 2*(x*y)^0.5*(skewness-beta)^alpha
func invtCalc(x: Int, y: Int) = {
    let sk = skewness(x, y)
    fraction(
        x + y,
        scale8,
        pow(sk, scale8Digits, alpha, alphaDigits, scale8Digits, UP)
    ) + 2 * fraction(
        pow(fraction(x, y, scale8), 0, 5, 1, scale8Digits / 2, DOWN),
        pow(sk - beta, scale8Digits, alpha, alphaDigits, scale8Digits, DOWN),
        scale8
    )
}

func calculateHowManySendA(amtToSendEstm: Int, minTokenReceiveAmount: Int, amtA: Int, amtB: Int, tokenReceiveAmount: Int) = {
    let slippageValue = scale8 - scale8 * 1 / 10000000 # 0.000001% of slippage
    let deltaMaxAndMinSendValue = amtToSendEstm - minTokenReceiveAmount

    let amtToSendStep1 = amtToSendEstm - 1 * deltaMaxAndMinSendValue / 5
    let amtToSendStep2 = amtToSendEstm - 2 * deltaMaxAndMinSendValue / 5
    let amtToSendStep3 = amtToSendEstm - 3 * deltaMaxAndMinSendValue / 5
    let amtToSendStep4 = amtToSendEstm - 4 * deltaMaxAndMinSendValue / 5
    let amtToSendStep5 = amtToSendEstm - 5 * deltaMaxAndMinSendValue / 5

    let y = amtB + tokenReceiveAmount
    let invtNew = invtCalc(amtA - amtToSendEstm, y)
    let invtEstmRatio =
                fraction(invt, scale8, invtNew)

    # before start check last condition. If it wrong we can stop
    if invtCalc(amtA - amtToSendStep5, y) - invt <= 0 then
        throw("minTokenReceiveAmount too large.err1")
    else if invtEstmRatio > slippageValue && invtNew - invt > 0 then
        amtToSendEstm
    else if invtCalc(amtA - amtToSendStep1, y) - invt > 0 then
        amtToSendStep1 * (feeScaleDelimiter - fee) / feeScaleDelimiter
    else if invtCalc(amtA - amtToSendStep2, y) - invt > 0 then
        amtToSendStep2 * (feeScaleDelimiter - fee) / feeScaleDelimiter
    else if invtCalc(amtA - amtToSendStep3, y) - invt > 0 then
        amtToSendStep3 * (feeScaleDelimiter - fee) / feeScaleDelimiter
    else if invtCalc(amtA - amtToSendStep4, y) - invt > 0 then
        amtToSendStep4 * (feeScaleDelimiter - fee) / feeScaleDelimiter
    else if invtCalc(amtA - amtToSendStep5, y) - invt > 0 then
        amtToSendStep5 * (feeScaleDelimiter - fee) / feeScaleDelimiter
    else
        throw("something went wrong while working with amountToSendStep. err2")
}

func calculateHowManySendB(amtToSendEstm: Int, minTokenReceiveAmount: Int, amtA: Int, amtB: Int, tokenReceiveAmount: Int) = {
    let slippageValue = scale8 - scale8 * 1 / 10000000 # 0.000001% of slippage
    let deltaMaxAndMinSendValue = amtToSendEstm - minTokenReceiveAmount
    let amtToSendStep1 = amtToSendEstm - 1 * deltaMaxAndMinSendValue / 5
    let amtToSendStep2 = amtToSendEstm - 2 * deltaMaxAndMinSendValue / 5
    let amtToSendStep3 = amtToSendEstm - 3 * deltaMaxAndMinSendValue / 5
    let amtToSendStep4 = amtToSendEstm - 4 * deltaMaxAndMinSendValue / 5
    let amtToSendStep5 = amtToSendEstm - 5 * deltaMaxAndMinSendValue / 5

    let x = amtA + tokenReceiveAmount
    let invtNew = invtCalc(x, amtB - amtToSendEstm)
    let invtEstmRatio =
                fraction(invt, scale8, invtNew)

    # before start check last condition. If it wrong we can stop
    if invtCalc(x, amtB - amtToSendStep5) - invt <= 0 then
        throw("minTokenReceiveAmount too large.err1")
    else if invtEstmRatio > slippageValue && invtNew - invt > 0 then
        amtToSendEstm
    else if invtCalc(x, amtB - amtToSendStep1) - invt > 0 then
        amtToSendStep1 * (feeScaleDelimiter - fee) / feeScaleDelimiter
    else if invtCalc(x, amtB - amtToSendStep2) - invt > 0 then
        amtToSendStep2 * (feeScaleDelimiter - fee) / feeScaleDelimiter
    else if invtCalc(x, amtB - amtToSendStep3) - invt > 0 then
        amtToSendStep3 * (feeScaleDelimiter - fee) / feeScaleDelimiter
    else if invtCalc(x, amtB - amtToSendStep4) - invt > 0 then
        amtToSendStep4 * (feeScaleDelimiter - fee) / feeScaleDelimiter
    else if invtCalc(x, amtB - amtToSendStep5) - invt > 0 then
        amtToSendStep5 * (feeScaleDelimiter - fee) / feeScaleDelimiter
    else
        throw("something went wrong while working with amountToSendStep. err2")
}

func getAssetInfo(assetId: ByteVector|Unit) = match assetId {
    case id: ByteVector =>
        let stringId = id.toBase58String()
        let info = assetInfo(id).valueOrErrorMessage("Asset " + stringId + " doesn't exist")
        (stringId, info.name, info.decimals)
    case waves: Unit => ("WAVES", "WAVES", 8)
}

func suspend(cause: String) = [
    BooleanEntry(kActive, false),
    StringEntry(kCause, cause)
]

func stakingFee(assetId: ByteVector) = 9 * assetInfo(assetId).value().minSponsoredFee.value() # sponsored fee for invoke called from scripted account
# let stakingFeeInUSDN = 9 * assetInfo(USDN).value().minSponsoredFee.value()

func deductStakingFee(amtA: Int, amtB: Int) =
    #if assetIdB == unit then (amtA, amtB - 900000)
    #else if assetIdA == unit then (amtA - 900000, amtB) else
    if stakingAssets.containsElement(assetIdB) then {
        let deduct = stakingFee(assetIdB.value())
        let resB = amtB - deduct
        if resB <= 0 then
            throw("Insufficient amount " + amtB.toString() + " " + assetNameB
                + " to deduct staking fee " + deduct.toString())
        else (amtA, resB)
    } else if stakingAssets.containsElement(assetIdA) then {
        let deduct = stakingFee(assetIdA.value())
        let resA = amtA - deduct
        if resA <= 0 then
            throw("Insufficient amount " + amtA.toString() + " " + assetNameA
                + " to deduct staking fee " + deduct.toString())
        else (resA, amtB)
    } else (amtA, amtB)

func throwIsActive() = throw("DApp is already active")
func throwIsInactive() = throw("DApp is inactive at this moment")
func throwOnlyAdmin() = throw("Only adm can call this function")
func throwAssets() = throw("Incorrect assets attached. Expected: " + strAssetIdA + " and " + strAssetIdB)
func throwThreshold(threshold: Int, amtA: Int, amtB: Int) = throw("New balance in assets of the DApp is less than threshold " + threshold.toString()
    + ": " + amtA.toString() + " " + assetNameA + ", " + amtB.toString() + " " + assetNameB)
func throwInsufficientAvailableBalance(amt: Int, available: Int, assetName: String) = throw("Insufficient DApp balance to pay "
    + amt.toString() + " " + assetName + " due to staking. Available: "
    + available.toString() + " " + assetName +  ". Please contact support in Telegram: https://t.me/swopfisupport")
func throwInsufficientAvailableBalances(amtA: Int, amtB: Int) = throw("Insufficient DApp balance to pay "
    + amtA.toString() + " " + assetNameA + " and "  + amtB.toString() + " " + assetNameB
    + " due to staking. Available: "
    + availBalA.toString() + " " + assetNameA + " and "  + availBalB.toString() + " " + assetNameB
    +  ". Please contact support in Telegram: https://t.me/swopfisupport")
func suspendSuspicious() = suspend("Suspicious state. Actual balances: " + balA.toString() + " " + assetNameA + ", " + balB.toString() + " " + assetNameB
        + ". State: " + accountBalance(assetIdA).toString() + " " + assetNameA + ", " + accountBalance(assetIdB).toString() + " " + assetNameB)

@Callable(i)
func init() = {
    let (pmtAmountA, pmtAssetIdA) = (i.payments[0].amount, i.payments[0].assetId)
    let (pmtAmountB, pmtAssetIdB) = (i.payments[1].amount, i.payments[1].assetId)
    let (pmtStrAssetIdA, pmtAssetNameA, pmtDecimalsA) = getAssetInfo(pmtAssetIdA)
    let (pmtStrAssetIdB, pmtAssetNameB, pmtDecimalsB) = getAssetInfo(pmtAssetIdB)

    if this.getBoolean(kActive).isDefined() then
        throwIsActive()
    else if pmtAssetIdA == pmtAssetIdB then
        throw("Assets must be different")
    else {
        let shareName = "s" + pmtAssetNameA.take(7) + "_" + pmtAssetNameB.take(7)
        let shareDescription = "ShareToken of SwopFi protocol for " + pmtAssetNameA + " and " + pmtAssetNameB + " at address " + this.toString()

        # we save 16 digit info with using scale8Digits in pow functions. We divide /scale8 to delete 8 digits and another 8 digits we define as digits in shareToken in Issue tx
        let shareDecimals = (pmtDecimalsA + pmtDecimalsB) / 2
        let shareInitialSupply = fraction(
            pow(pmtAmountA, pmtDecimalsA, 5, 1, pmtDecimalsA, HALFDOWN),
            pow(pmtAmountB, pmtDecimalsB, 5, 1, pmtDecimalsB, HALFDOWN),
            pow(10, 0, shareDecimals, 0, 0, HALFDOWN)
        )
        let shareIssue = Issue(shareName, shareDescription, shareInitialSupply, shareDecimals, true)
        let shareIssueId = shareIssue.calculateAssetId()

        let invtCalculated = invtCalc(pmtAmountA, pmtAmountB)
        [
            StringEntry(kVersion, version),
            BooleanEntry(kActive, true),
            StringEntry(kAssetIdA, pmtStrAssetIdA),
            StringEntry(kAssetIdB, pmtStrAssetIdB),
            IntegerEntry(kBalanceA, pmtAmountA),
            IntegerEntry(kBalanceB, pmtAmountB),
            IntegerEntry(kInvariant, invtCalculated),
            IntegerEntry(kCommission, fee),
            IntegerEntry(kFeeScaleDelimiter, feeScaleDelimiter),
            shareIssue,
            StringEntry(kShareAssetId, shareIssueId.toBase58String()),
            IntegerEntry(kShareAssetSupply, shareInitialSupply),
            ScriptTransfer(i.caller, shareInitialSupply, shareIssueId)
        ]
    }
}

# if someone ready to replenish in both tokens. It's necessary to bring tokens amount in proportion according to dApp state
@Callable(i)
func replenishWithTwoTokens(slippageTolerance: Int) = {
    let pmtAssetIdA = i.payments[0].assetId
    let pmtAssetIdB = i.payments[1].assetId
    # block for accounting the cost of commissions for staking operations
    let (pmtAmountA, pmtAmountB) = deductStakingFee(i.payments[0].amount, i.payments[1].amount)

    # fraction should be equal 1(multiple by 1000) if depositor replenish with proportion according to actual price
    let tokenRatio = fraction(balA, scale8, pmtAmountA)
                    .fraction(scale3, balB.fraction(scale8, pmtAmountB))

    let ratioShareTokensInA = fraction(pmtAmountA, scale8, balA)
    let ratioShareTokensInB = fraction(pmtAmountB, scale8, balB)
    let shareTokenToPayAmount = min([ratioShareTokensInA, ratioShareTokensInB]).fraction(shareAssetSupply, scale8)

    let invtCalculated = invtCalc(balA + pmtAmountA, balB + pmtAmountB)

    if !isActive then
        throwIsInactive()
    else if slippageTolerance < 0 || slippageTolerance > slippageToleranceDelimiter then
        throw("Slippage tolerance must be between 0 and " + slippageToleranceDelimiter.toString() + " inclusively. Actual: " + slippageTolerance.toString())
    else if i.payments.size() != 2 then
        throw("Two attached assets expected")
    else if pmtAssetIdA != assetIdA || pmtAssetIdB != assetIdB then
        throwAssets()
    else if tokenRatio < (scale3 * (slippageToleranceDelimiter - slippageTolerance)) / slippageToleranceDelimiter
            || tokenRatio > (scale3 * (slippageToleranceDelimiter + slippageTolerance)) / slippageToleranceDelimiter then
        throw("Incorrect assets amount: amounts must have the contract ratio")
    else if shareTokenToPayAmount == 0 then
        throw("Too small amount to replenish")
    else if !hasEnoughBalance then
        [
            ScriptTransfer(i.caller, pmtAmountA, pmtAssetIdA),
            ScriptTransfer(i.caller, pmtAmountB, pmtAssetIdB)
        ] ++ suspendSuspicious()
    else [
        IntegerEntry(kBalanceA, balA + pmtAmountA),
        IntegerEntry(kBalanceB, balB + pmtAmountB),
        IntegerEntry(kShareAssetSupply, shareAssetSupply + shareTokenToPayAmount),
        IntegerEntry(kInvariant, invtCalculated),
        Reissue(shareAssetId, shareTokenToPayAmount, true),
        ScriptTransfer(i.caller, shareTokenToPayAmount, shareAssetId)
    ]
}

# if someone ready to replenish in one token
@Callable(i)
func replenishWithOneToken(virtualSwapTokenPay: Int, virtualSwapTokenGet: Int) = {
    let (pmtAmount, pmtAssetId) = (i.payments[0].amount, i.payments[0].assetId)

    let pmtMinThreshold = 5000000
    let thresholdValueForMinTolerance = 50000000
    let tolerance = if pmtAmount < thresholdValueForMinTolerance then 100000 else 1

    let slippageValueMinForReplenish = scale8 - scale8 * tolerance / 10000000 # if pmtAmount > 50.000000 then slippage = 0.000001%
    let slippageValueMaxForReplenish = scale8 + scale8 * tolerance / 10000000

    let slippageValueMinForSwap = scale8 - scale8 * 1 / 10000000
    let slippageValueMaxForSwap = scale8 + scale8 * 1 / 10000000

    if !isActive then
        throwIsInactive()
    else if pmtAmount < pmtMinThreshold then
        throw("Payment amount " + pmtAmount.toString() + " does not exceed the minimum amount of " + pmtMinThreshold.toString() + " tokens")
    else if i.payments.size() != 1 then
        throw("One attached payment expected")
    else if !hasEnoughBalance then
        suspendSuspicious() :+ ScriptTransfer(i.caller, pmtAmount, pmtAssetId)
    else if pmtAssetId == assetIdA then {
        let (virtReplA, virtReplB) = deductStakingFee(pmtAmount - virtualSwapTokenPay, virtualSwapTokenGet)
        let balAfterSwapA = balA + virtualSwapTokenPay
        let balAfterSwapB = balB - virtualSwapTokenGet

        let invtNew = invtCalc(balAfterSwapA, balAfterSwapB)
        let invtEstmRatio = fraction(invt, scale8, invtNew)

        if invtEstmRatio > slippageValueMinForSwap && invtNew - invt >= 0 then {
            let ratioVirtualBalanceToVirtualReplenish = fraction(balAfterSwapA, scale8 * scale8, balAfterSwapB)
                                                        / fraction(virtReplA, scale8, virtReplB)
            if ratioVirtualBalanceToVirtualReplenish < slippageValueMinForReplenish || ratioVirtualBalanceToVirtualReplenish > slippageValueMaxForReplenish then
                throw("swop with virtualSwapTokenPay and virtualSwapTokenGet possible, but ratio after virtual swap incorrect")
            else {
                # fee for staking operation
                let ratioShareTokensInA = virtReplA.fraction(scale8, balAfterSwapA)
                let ratioShareTokensInB = virtReplB.fraction(scale8, balAfterSwapB)

                let shareTokenToPayAmount = min([ratioShareTokensInA, ratioShareTokensInB]).fraction(shareAssetSupply, scale8)
                let invtCalculated = invtCalc(balA + pmtAmount, balB)

                let newBalanceA = balA + pmtAmount
                let newBalanceB = balB
                let dAppTHoldAmount = fraction(newBalanceA + newBalanceB, dAppTHold, 2 * dAppTHoldScale)

                if newBalanceA < dAppTHoldAmount || newBalanceB < dAppTHoldAmount then
                    throwThreshold(dAppTHoldAmount, newBalanceA, newBalanceB)
                else
                [
                    Reissue(shareAssetId, shareTokenToPayAmount, true),
                    ScriptTransfer(i.caller, shareTokenToPayAmount, shareAssetId),
                    IntegerEntry(kShareAssetSupply, shareAssetSupply + shareTokenToPayAmount),
                    IntegerEntry(kBalanceA, newBalanceA),
                    IntegerEntry(kInvariant, invtCalculated)
                ]
            }
        } else
            throw("incorrect virtualSwapTokenPay or virtualSwapTokenGet value")
    } else if pmtAssetId == assetIdB then {
        let (virtReplA, virtReplB) = deductStakingFee(virtualSwapTokenGet, pmtAmount - virtualSwapTokenPay)
        let balAfterSwapA = balA - virtualSwapTokenGet
        let balAfterSwapB = balB + virtualSwapTokenPay

        let invtNew = invtCalc(balAfterSwapA, balAfterSwapB)
        let invtEstmRatio = fraction(invt, scale8,invtNew)

        if invtEstmRatio > slippageValueMinForSwap && invtNew - invt >= 0 then {
            let ratioVirtualBalanceToVirtualReplenish = fraction(balAfterSwapA, scale8 * scale8, balAfterSwapB)
                                                        / fraction(virtReplA, scale8, virtReplB)

            if ratioVirtualBalanceToVirtualReplenish < slippageValueMinForReplenish || ratioVirtualBalanceToVirtualReplenish > slippageValueMaxForReplenish then
                throw("swop with virtualSwapTokenPay and virtualSwapTokenGet possible, but ratio after virtual swap incorrect")
            else {
                let ratioShareTokensInA = virtReplA.fraction(scale8, balAfterSwapA)
                let ratioShareTokensInB = virtReplB.fraction(scale8, balAfterSwapB)

                let shareTokenToPayAmount = min([ratioShareTokensInA, ratioShareTokensInB]).fraction(shareAssetSupply, scale8)
                let invtCalculated = invtCalc(balA, balB + pmtAmount)

                let newBalanceA = balA
                let newBalanceB = balB + pmtAmount
                let dAppTHoldAmount = fraction(newBalanceA + newBalanceB, dAppTHold, 2 * dAppTHoldScale)
                if newBalanceA < dAppTHoldAmount || newBalanceB < dAppTHoldAmount then
                    throwThreshold(dAppTHoldAmount, newBalanceA, newBalanceB)
                else
                [
                    Reissue(shareAssetId, shareTokenToPayAmount, true),
                    ScriptTransfer(i.caller, shareTokenToPayAmount, shareAssetId),
                    IntegerEntry(kShareAssetSupply, shareAssetSupply + shareTokenToPayAmount),
                    IntegerEntry(kBalanceB, newBalanceB),
                    IntegerEntry(kInvariant, invtCalculated)
                ]
            }
        } else
            throw("incorrect virtualSwapTokenPay or virtualSwapTokenGet value")
    } else
        throwAssets()
}

@Callable(i)
func withdraw() = {
    let (pmtAmount, pmtAssetId) = (i.payments[0].amount, i.payments[0].assetId)

    # block for accounting the cost of commissions for staking operations
    let (amtToPayA, amtToPayB) = deductStakingFee(pmtAmount.fraction(balA, shareAssetSupply), pmtAmount.fraction(balB, shareAssetSupply))

    let invtCalculated = invtCalc(balA - amtToPayA, balB - amtToPayB)

    if !isActive then
        throwIsInactive()
    else if i.payments.size() != 1 then
        throw("One attached payment expected")
    else if pmtAssetId != shareAssetId then
        throw("Incorrect asset attached. Expected: " + shareAssetId.toBase58String())
    else if !hasEnoughBalance then
        suspendSuspicious() :+ ScriptTransfer(i.caller, pmtAmount, pmtAssetId)
    else if amtToPayA > availBalA || amtToPayB > availBalB then
        throwInsufficientAvailableBalances(amtToPayA, amtToPayB)
    else [
        IntegerEntry(kBalanceA, balA - amtToPayA),
        IntegerEntry(kBalanceB, balB - amtToPayB),
        IntegerEntry(kShareAssetSupply, shareAssetSupply - pmtAmount),
        IntegerEntry(kInvariant, invtCalculated),
        Burn(shareAssetId, pmtAmount),
        ScriptTransfer(i.caller, amtToPayA, assetIdA),
        ScriptTransfer(i.caller, amtToPayB, assetIdB)
    ]
}

@Callable(i)
func exchange(estimatedAmountToReceive: Int, minAmountToReceive: Int) = {
    let (pmtAmount, pmtAssetId) = (i.payments[0].amount, i.payments[0].assetId)

    if !isActive then
        throwIsInactive()
    else if estimatedAmountToReceive <= 0 then
        throw("Estimated amount must be positive. Actual: " + estimatedAmountToReceive.toString())
    else if minAmountToReceive > estimatedAmountToReceive then
        throw("Minimal amount can't be greater than estimated. Estimated: " + estimatedAmountToReceive.toString() + ". Minimal: " + minAmountToReceive.toString())
    else if i.payments.size() != 1 then
        throw("One attached payment expected")
    else if !hasEnoughBalance then
        suspendSuspicious() :+ ScriptTransfer(i.caller, pmtAmount, pmtAssetId)
    else if pmtAssetId == assetIdA then {
        let assetIdSend = assetIdB
        let amtWithoutFee = calculateHowManySendB(estimatedAmountToReceive, minAmountToReceive, balA, balB, pmtAmount)
        let amtWithFee = fraction(amtWithoutFee, feeScaleDelimiter - fee, feeScaleDelimiter)
        let governanceReward = fraction(amtWithoutFee, feeGovernance, feeScaleDelimiter)

        let newBalanceA = balA + pmtAmount
        let newBalanceB = balB - amtWithFee - governanceReward
        let dAppTHoldAmount = fraction(newBalanceA + newBalanceB, dAppTHold, 2 * dAppTHoldScale)

        if newBalanceA < dAppTHoldAmount || newBalanceB < dAppTHoldAmount then
            throwThreshold(dAppTHoldAmount, newBalanceA, newBalanceB)
        else if newBalanceA <= stakedAmountA || newBalanceB <= stakedAmountB then
            throwInsufficientAvailableBalance(amtWithFee, availBalB, assetNameB)
        else
            # successful execution result is updating information about actual balance and supply into the state and transfer tokens to the caller
            [
                IntegerEntry(kBalanceA, newBalanceA),
                IntegerEntry(kBalanceB, newBalanceB),
                IntegerEntry(kInvariant, invtCalc(balA + pmtAmount, balB - amtWithFee - governanceReward)),
                ScriptTransfer(i.caller, amtWithFee, assetIdSend),
                ScriptTransfer(govAddr, governanceReward, assetIdSend)
            ]
    } else if pmtAssetId == assetIdB then {
        let assetIdSend = assetIdA
        let amtWithoutFee = calculateHowManySendA(estimatedAmountToReceive, minAmountToReceive, balA, balB, pmtAmount)
        let amtWithFee = fraction(amtWithoutFee, feeScaleDelimiter - fee, feeScaleDelimiter)
        let governanceReward = fraction(amtWithoutFee, feeGovernance, feeScaleDelimiter)

        let newBalanceA = balA - amtWithFee - governanceReward
        let newBalanceB = balB + pmtAmount
        let dAppTHoldAmount = fraction(newBalanceA + newBalanceB, dAppTHold, 2 * dAppTHoldScale)

        if newBalanceA < dAppTHoldAmount || newBalanceB < dAppTHoldAmount then
            throwThreshold(dAppTHoldAmount, newBalanceA, newBalanceB)
        else if newBalanceA <= stakedAmountA || newBalanceB <= stakedAmountB then
            throwInsufficientAvailableBalance(amtWithFee, availBalA, assetNameA)
        else
            # successful execution result is updating information about actual balance and supply into the state and transfer tokens to the caller
            [
                IntegerEntry(kBalanceA, newBalanceA ),
                IntegerEntry(kBalanceB,  newBalanceB),
                IntegerEntry(kInvariant, invtCalc( balA - amtWithFee - governanceReward,balB + pmtAmount)),
                ScriptTransfer(i.caller, amtWithFee, assetIdSend),
                ScriptTransfer(govAddr, governanceReward, assetIdSend)
            ]
    } else
        throwAssets()
}

@Callable(i)
func shutdown() =
    if !isActive then
        throw("DApp is already suspended. Cause: " + this.getString(kCause).valueOrElse("the cause wasn't specified"))
    else if ![adm1, adm2, adm3, admStartStop].containsElement(i.callerPublicKey) then
        throwOnlyAdmin()
    else
        suspend("Paused by adm")

@Callable(i)
func activate() =
    if isActive then
        throwIsActive()
    else if ![adm1, adm2, adm3, admStartStop].containsElement(i.callerPublicKey) then
        throwOnlyAdmin()
    else [
        BooleanEntry(kActive, true),
        DeleteEntry(kCause)
    ]


@Callable(i)
func takeIntoAccountExtraFunds(amtLeaveA: Int, amtLeaveB: Int) = {
    let uncountedA = accBalWithStakedA - balA
    let uncountedB = accBalWithStakedB - balB
    let enrollA = uncountedA - if stakingAssets.containsElement(assetIdA) then amtLeaveA else 0
    let enrollB = uncountedB - if stakingAssets.containsElement(assetIdB) then amtLeaveB else 0

    if !isActive then
            throwIsInactive()
    else if i.caller != this then
        throwOnlyAdmin()
    else if amtLeaveA < 0 || amtLeaveB < 0 then
        throw("Arguments 'amountLeaveA' and 'amountLeaveB' cannot be negative. Actual: " + amtLeaveA.toString() + " and " + amtLeaveB.toString())
    else if uncountedA < 0 || uncountedB < 0 then
        suspend("Enroll amount negative")
    else if enrollA < 0 || enrollB < 0 then
        throw("Too large amountLeave")
    else
        [
            IntegerEntry(kBalanceA, balA + enrollA),
            IntegerEntry(kBalanceB, balB + enrollB)
        ]
}

@Verifier(tx)
func verify() = match tx {
    case invoke: InvokeScriptTransaction =>
        let callTakeIntoAccount = invoke.dApp == this && invoke.function == "takeIntoAccountExtraFunds"
        let callStakingUsdnOrNsbt =
            invoke.dApp == stakingAddressUsdnAndNsbt
            && (
                (["lockNeutrino", "lockNsbt"].containsElement(invoke.function) && invoke.payments.size() == 1)
                || (["unlockNeutrino", "unlockNsbt"].containsElement(invoke.function) && invoke.payments.size() == 0)
            )
        let callStakingDeFo =
            invoke.dApp == stakingAddressUsdnAndNsbt
            && (
                (invoke.function == "startStaking" && invoke.payments.size() == 1)
                || (invoke.function == "stopStaking" && invoke.payments.size() == 0)
            )
        let signedByAdmin =
            sigVerify(tx.bodyBytes, tx.proofs[0], adm1)
            || sigVerify(tx.bodyBytes, tx.proofs[0], adm2)
            || sigVerify(tx.bodyBytes, tx.proofs[0], adm3)
            || sigVerify(tx.bodyBytes, tx.proofs[0], admStaking)

        (callTakeIntoAccount || callStakingUsdnOrNsbt || callStakingDeFo) && signedByAdmin
    case _ => {
        let adm1Signed = if sigVerify(tx.bodyBytes, tx.proofs[0], adm1) then 1 else 0
        let adm2Signed = if sigVerify(tx.bodyBytes, tx.proofs[1], adm2) then 1 else 0
        let adm3Signed = if sigVerify(tx.bodyBytes, tx.proofs[2], adm3) then 1 else 0
        adm1Signed + adm2Signed + adm3Signed >= 2
    }
}
