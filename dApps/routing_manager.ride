{-# STDLIB_VERSION 6 #-}
{-# CONTENT_TYPE DAPP #-}
{-# SCRIPT_TYPE ACCOUNT #-}

let kActive = "active"
let kCause = "shutdown_cause"
let kPoolInOracle = "pool_"
let kUserSwopInGov = "_SWOP_amount"
let kDiscounts = "discounts"
let kDiscountValues = "discount_values"
let kRoutingFee = "routing_fee"
let kAdminPubKey1 = "admin_pub_1"
let kAdminPubKey2 = "admin_pub_2"
let kAdminPubKey3 = "admin_pub_3"
let kMoneyBoxAddress = "money_box_address"
let kGovAddress = "governance_address"
let kFee = "commission"
let kMultyAssetBalance = "_balance"


let digits8 = 8
let scale8 = 100000000
let scale16 = 10000000000000000
let alpha = 50 # model coefficient alpha = 0.15 with 2 digits
let alphaDigits = 2
let beta = 4600000000000000 # model coefficient beta = 0.46 with 16 digits

let oracle = Address(base58'3PEbqViERCoKnmcSULh6n2aiMvUdSQdCsom')

func getBase58FromOracle(key: String) = match oracle.getString(key) {
    case string: String => string.fromBase58String()
    case nothing => throw(key + "is empty")
}

let adminPubKey1 = getBase58FromOracle(kAdminPubKey1)
let adminPubKey2 = getBase58FromOracle(kAdminPubKey2)
let adminPubKey3 = getBase58FromOracle(kAdminPubKey3)
let moneyBoxAddress = Address(getBase58FromOracle(kMoneyBoxAddress))
let govAddress = Address(getBase58FromOracle(kGovAddress))


let active = this.getBoolean(kActive).valueOrElse(true)
let feeScale6 = 1000000

func isActive() = if active then unit else throw("DApp is inactive at this moment")
func isAdminCall(i:Invocation) = if [adminPubKey1, adminPubKey2, adminPubKey3].containsElement(i.callerPublicKey) then unit
        else throw("Only admin can call this function")
func throwIsActive() = throw("DApp is already active")

func suspend(cause: String) = [
    BooleanEntry(kActive, false),
    StringEntry(kCause, cause)
]

# skewness = 0.5*(x/y+y/x)
# Each fraction multiple by scale in order to avoid zeroing
func skewness(x: Int, y: Int) = (scale16.fraction(x, y) + scale16.fraction(y, x)) / 2

# Calculate (x+y)*skewness^(-alpha) + 2*(x*y)^0.5*(skewness-beta)^alpha
func invariantCalc(x: Int, y: Int) = {
    let sk = skewness(x, y)
    fraction(
        x + y,
        scale16,
        pow(sk, digits8, alpha, alphaDigits, digits8, CEILING)
    ) + 2 * fraction(
        pow(fraction(x.toBigInt(), y.toBigInt(), scale8.toBigInt()), 0, 5.toBigInt(), 1, digits8 / 2, DOWN).toInt(),
        pow(sk - beta, digits8, alpha, alphaDigits, digits8, DOWN),
        scale8
    )
}

func calculateFeeDiscount(userAddr: Address) = {
    let swopAmount = govAddress.getInteger(userAddr.toString() + kUserSwopInGov).valueOrElse(0)
    let discountValues = oracle.getStringValue(kDiscountValues).split(",")
    let discounts = oracle.getStringValue(kDiscounts).split(",")

    if swopAmount >= parseIntValue(discountValues[0]) && swopAmount < parseIntValue(discountValues[1]) then
        feeScale6 - parseIntValue(discounts[0])
    else if swopAmount >= parseIntValue(discountValues[1]) && swopAmount < parseIntValue(discountValues[2]) then
        feeScale6 - parseIntValue(discounts[1])
    else if swopAmount >= parseIntValue(discountValues[2]) && swopAmount < parseIntValue(discountValues[3]) then
        feeScale6 - parseIntValue(discounts[2])
    else if swopAmount >= parseIntValue(discountValues[3]) && swopAmount < parseIntValue(discountValues[4]) then
        feeScale6 - parseIntValue(discounts[3])
    else if swopAmount >= parseIntValue(discountValues[4]) then
        feeScale6 - parseIntValue(discounts[4])
    else
        feeScale6
}

# Checking possibility of the route
func checkPossibility(caller: Address, pmtAmount: Int, minAmountToReceive: Int, exchangers: List[String], exchangersType: List[String],routingAssetsKeys:List[String], args1: List[Int], args2: List[Int]) ={

    let feeDiscount = calculateFeeDiscount(caller)
    func checkTrade(amountGet: Int, i: Int) = {
        if i > exchangers.size() - 1 then 
            amountGet 
        else
            if exchangersType[i] == "curveMultyFlat" then
                strict inv = invoke(exchangers[i].addressFromStringValue(), "getDy", [routingAssetsKeys[i * 2], routingAssetsKeys[i * 2 + 1], amountGet, caller.toString()], []).exactAs[(Int, Int)]
                inv._1
            else
                let dApp1TokenPay = exchangers[i].addressFromStringValue().getIntegerValue(routingAssetsKeys[i * 2])
                let dApp1TokenGet = exchangers[i].addressFromStringValue().getIntegerValue(routingAssetsKeys[i * 2 + 1])
                if exchangersType[i] == "cpmm" then
                    let fee = exchangers[i].addressFromStringValue().getIntegerValue(kFee)
                    let getFromTradeWithoutFee = fraction(dApp1TokenGet, amountGet, amountGet + dApp1TokenPay)
                    fraction(getFromTradeWithoutFee, feeScale6 - fee.fraction(feeDiscount, feeScale6), feeScale6)
                else if exchangersType[i] == "flat" then
                    let invariant = invariantCalc(dApp1TokenPay, dApp1TokenGet)
                    let minAmountToReceiveTrade = args2[i]
                    let invariantNew = invariantCalc(dApp1TokenPay + amountGet, dApp1TokenGet - minAmountToReceiveTrade)
                    if invariantNew >= invariant then minAmountToReceiveTrade else throw("Flat contract: invariantNew < invariant")
                else
                    throw("Incorrect exchange type")
    }
    let amount = FOLD<3>([0,1,2], pmtAmount, checkTrade)
    amount >= minAmountToReceive
}

# exchangers — exchangers list(2 exchangers for now)
# exchangersType — exchnagers types list (flat/cpmm) in exchangers order
# args1, args2 — args pairs for invoke function exchange in exchangers order
# routingAssetsKeys - assets keys pairs for each exchange or Assets ids in case multy curve flat
# Example if pay in asset B of ex1 and want to get asset B of ex2 than list will be
# [B_asset_balance, A_asset_balance, A_asset_balance, B_asset_balance]
# minAmountToReceive — minimal amount to recive with routing fee
@Callable(i)
func routingTrade(exchangers: List[String], exchangersType: List[String], args1: List[Int], args2: List[Int], routingAssetsKeys:List[String], minAmountToReceive: Int) = isActive().valueOrElse({
    let (pmtAmount,pmtAssetId) = (i.payments[0].amount, i.payments[0].assetId)

    if !checkPossibility(i.caller, pmtAmount,minAmountToReceive,exchangers,exchangersType,routingAssetsKeys,args1,args2) then
        throw("Check transaction possibilty: declined")
    else if !oracle.getString(kPoolInOracle + exchangers[0]).isDefined() || !oracle.getString(kPoolInOracle + exchangers[1]).isDefined() then
        throw("Pool is not in oracle")
    else
        let routingSize = exchangers.size()
        func exchangeFold(accumulated: (Int,Int,ByteVector), exchanger: String) = {
            let (exchangeCounter, amountsWithFee, assetsIdSend) = accumulated
            let (pmtAssetIdRout, pmtAmountRout) =
                if exchangeCounter == 0 then
                    (pmtAssetId, pmtAmount)
                else
                    (assetsIdSend, amountsWithFee)
            if (exchangeCounter >= 0  &&  exchangeCounter < routingSize) == true then
                let exchangerAddress = exchanger.addressFromStringValue()
                strict inv = 
                    if exchangersType[exchangeCounter] == "cpmm" then
                        invoke(exchangerAddress, "callFunction", ["exchange", [args1[exchangeCounter].toString()]], [AttachedPayment(pmtAssetIdRout, pmtAmountRout)])
                    else if exchangersType[exchangeCounter] == "flat" then
                        invoke(exchangerAddress, "exchange", [args1[exchangeCounter], args2[exchangeCounter]], [AttachedPayment(pmtAssetIdRout, pmtAmountRout)])
                    else if exchangersType[exchangeCounter] == "curveMultyFlat" then
                        invoke(exchangerAddress, "exchange", [routingAssetsKeys[exchangeCounter * 2 + 1], args2[exchangeCounter]], [AttachedPayment(pmtAssetIdRout, pmtAmountRout)])
                    else
                        throw("Incorrect exchange type")
                let (amountWithFee, assetIdSend) = match (inv) {
                    case inv:List[Any] => 
                        match (inv[0], inv[1]) {
                            case r:(Int, ByteVector|Unit) => (r._1, r._2)
                            case _ => throw("Incorrect invoke result: expected amountWithFee and assetsIdSend")
                        } 
                    case inv : (Int, ByteVector|Unit) => (inv._1, inv._2)
                    case _ => throw("Incorrect invoke result: expected amountWithFee and assetsIdSend")
                }
                (exchangeCounter + 1, amountWithFee, assetIdSend)
            else
                accumulated
        }
        let (exchangeCounter, amount, assetIdSend) = FOLD<3>(exchangers, (0, 0, base58''), exchangeFold)
        if exchangeCounter == routingSize && amount >= minAmountToReceive then
            [
                ScriptTransfer(i.caller, amount, assetIdSend)
            ]
        else
            throw("routing != routingSize or amountToRecieve < minAmountToReceive")
})

@Callable(i)
func shutdown() = isAdminCall(i).valueOrElse({
    if !active then
        throw("DApp is already suspended. Cause: " + this.getString(kCause).valueOrElse("the cause wasn't specified"))
    else
        suspend("Paused by admin")
})

@Callable(i)
func activate() = isAdminCall(i).valueOrElse({
    if active then
        throwIsActive()
    else [
        BooleanEntry(kActive, true),
        DeleteEntry(kCause)
    ]
})

@Verifier(tx)
func verify() = {
    let multiSignedByAdmins = {
        let adminPubKey1Signed = if sigVerify(tx.bodyBytes, tx.proofs[0], adminPubKey1) then 1 else 0
        let adminPubKey2Signed = if sigVerify(tx.bodyBytes, tx.proofs[1], adminPubKey2) then 1 else 0
        let adminPubKey3Signed = if sigVerify(tx.bodyBytes, tx.proofs[2], adminPubKey3) then 1 else 0
        adminPubKey1Signed + adminPubKey2Signed + adminPubKey3Signed >= 2
    }
    match tx {
        case _ => multiSignedByAdmins
    }
}